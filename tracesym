#!/bin/sh

if [ $# -ne 2 ]; then
	echo "usage: $0 binary-path trace" > /dev/stderr
	exit 1
fi

./objdump -d "$1" | awk '
	BEGIN {
		maxdepth = 6
	}

	NR == FNR && /^0/ {
		gsub("[<>:]", "", $2)
		symbol = $2
	}

	NR == FNR && /^ / {
		text[$1] = symbol
	}

	NR != FNR {
		printf "%-98s # %" 2 * (maxdepth - depth) "s%s %s\n", $0, "", text[$1], stack
	}

	NR != FNR && jalr_delay != 0 {
		jalr_delay = 0
		stack = text[$1] " " stack
		depth++
		if (depth > maxdepth)
			maxdepth = depth
	}

	NR != FNR && $3 == "jalr" {
		jalr_delay = 1
	}

	NR != FNR && jr_delay != 0 {
		jr_delay = 0
		sub(/[^ ]+ /, "", stack)
		depth--
	}

	NR != FNR && $3 == "jr" && $4 == "ra" {
		jr_delay = 1
	}

' - "$2"

